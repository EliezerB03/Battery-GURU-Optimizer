apply plugin: 'com.android.application'
android {
    namespace = "com.guru.batteryoptimizer"
    compileSdk = 36
    defaultConfig {
        applicationId "com.guru.batteryoptimizer"
        minSdkVersion 33
        targetSdkVersion 35
        versionName "3.6"
        versionCode 360
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_25
        targetCompatibility JavaVersion.VERSION_25
    }
    tasks.register('copyWebAssets', Copy) {
        from '../../webapp'  
        into 'src/main/assets' 
    }
    preBuild.dependsOn copyWebAssets
    androidComponents {
        onVariants(selector().all()) { variant ->
            variant.outputs.forEach { output ->
                if (variant.buildType == "debug") {
                    output.outputFileName.set("bgo-app-debug.apk")
                }
                if (variant.buildType == "release") {
                    output.outputFileName.set("bgo-app-release.apk")
                }
            }
        }
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
    signingConfigs {
        release {
            def ksPath = System.getenv("KEYSTORE_PATH") ?: project.findProperty("android.injected.signing.store.file")
            if (ksPath) {
                storeFile file(ksPath)
                storePassword System.getenv("KEYSTORE_PASSWORD") ?: project.findProperty("android.injected.signing.store.password")
                keyAlias System.getenv("KEY_ALIAS") ?: project.findProperty("android.injected.signing.key.alias")
                keyPassword System.getenv("KEY_PASSWORD") ?: project.findProperty("android.injected.signing.key.password")
            } else {
                println "KEYSTORE is only configured for RELEASE Build"
            }
        }
    }
    buildTypes {
        release {
            signingConfig = signingConfigs.release
            debuggable = false
            minifyEnabled = true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
        debug {
            debuggable = true
        }
    }
}
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:1.7.1"
    implementation "androidx.webkit:webkit:1.15.0"
    implementation "androidx.core:core:1.17.0"
    implementation 'androidx.core:core-splashscreen:1.2.0'
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.3.0"
    implementation "com.github.topjohnwu.libsu:core:6.0.0"
}
try {
    def servicesJSON = file('google-services.json')
    if (servicesJSON.text) {
        apply plugin: 'com.google.gms.google-services'
    }
} catch(Exception e) {
    logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}